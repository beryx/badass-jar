import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'java-gradle-plugin'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'groovy'
    id "com.gradle.plugin-publish" version "0.9.10"
    id "com.github.ethankhall.semantic-versioning" version "1.1.0"
    id "com.github.ben-manes.versions" version "0.20.0"
    id "com.github.hierynomus.license" version "0.15.0"
}

project.version.with {
    major = badassJarVersionMajor as int
    minor= badassJarVersionMinor as int
    patch = badassJarVersionPatch as int
    if (project.hasProperty('badassJarVersionLabel')) {
        preRelease = badassJarVersionLabel
    }
    releaseBuild = Boolean.valueOf(badassJarReleaseBuild)
}
ext.badassJarVersion = project.version as String
ext.badassJarTag = Boolean.valueOf(badassJarReleaseBuild) ? "v$ext.badassJarVersion" : 'master'

group = 'org.beryx'
version = badassJarVersion

ext.asmVersion = '7.0'

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

sourceCompatibility = 1.8

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

[compileGroovy, compileTestGroovy]*.options*.encoding = defaultEncoding
[compileGroovy, compileTestGroovy]*.groovyOptions*.encoding = defaultEncoding

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs = ["src/main/java", "src/main/groovy"]

configurations {
    plugin.description = 'Plugin\'s dependencies'
    compile.extendsFrom plugin
}

dependencies {
    implementation gradleTestKit()
    implementation localGroovy()

    implementation 'org.javamodularity:moduleplugin:1.4.0'

    plugin 'com.github.javaparser:javaparser-core:3.6.0'
    plugin "org.ow2.asm:asm:$asmVersion"
    plugin "org.ow2.asm:asm-commons:$asmVersion"
    plugin "org.ow2.asm:asm-tree:$asmVersion"
    plugin "org.ow2.asm:asm-util:$asmVersion"
    plugin "org.ow2.asm:asm-analysis:$asmVersion"

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.2.10'
    testCompile 'org.objenesis:objenesis:3.0.1'
    testCompile('com.athaydes:spock-reports:1.6.1') { transitive = false}
}

shadowJar {
    configurations = [project.configurations.plugin]
    classifier = null
    dependencies {
        include(dependency('com.github.javaparser:javaparser-core:3.6.0'))
        include(dependency("org.ow2.asm:asm:$asmVersion"))
        include(dependency("org.ow2.asm:asm-commons:$asmVersion"))
        include(dependency("org.ow2.asm:asm-tree:$asmVersion"))
        include(dependency("org.ow2.asm:asm-util:$asmVersion"))
        include(dependency("org.ow2.asm:asm-anaysis:$asmVersion"))
    }
    relocate 'com.github.javaparser', 'org.beryx.jar.shadow.javaparser'
    relocate 'org.objectweb.asm', 'org.beryx.jar.shadow.asm'
}

jar.enabled = false
jar.dependsOn shadowJar

gradlePlugin {
    plugins {
        badassJar {
            id = 'org.beryx.jar'
            implementationClass = 'org.beryx.jar.BadassJarPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/beryx/badass-jar-plugin/'
    vcsUrl = 'https://github.com/beryx/badass-jar-plugin'
    description = 'A Gradle plugin that lets you create modular jars compatible with pre-Java 9 versions'
    tags = ['jar', 'jpms', 'module-info']

    plugins {
        jlink {
            id = 'org.beryx.jar'
            displayName = 'Badass Jar Plugin'
        }
    }
}


jar {
    manifest {
        attributes 'Implementation-Title': "${project.archivesBaseName}",
                'Implementation-Version': badassJarVersion
    }
}

license {
    header rootProject.file("license-header.txt")
    skipExistingHeaders true
    ignoreFailures false

    excludes(['**/*.properties'])
}
